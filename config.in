dnl Process this file with autoconf to produce a configure script.
AC_INIT(src/abcess.h)
AC_CONFIG_HEADER(temp/config.h:src/configh.in)

dnl Information about this version of INTERCAL.
PACKAGE_VERSION=0.27

dnl Checks for programs.
AC_PROG_YACC
AC_PROG_CC
dnl AC_PROG_LEX is checked by AC_DECL_YYTEXT below
AC_PROG_RANLIB
AC_CHECK_PROGS(INSTALLINFO,install-info,:)
AC_CHECK_PROGS(MANDB,mandb,:)

dnl Check the location of the Info dir file if we have install-info
if test ! INSTALLINFO = ":"; then
AC_MSG_CHECKING(the location of the Info dir file)
# Try the following locations in order:
INFODIRFILELOCATIONGUESSES=${infodir}/dir
INFODIRFILELOCATIONGUESSES="${INFODIRFILELOCATIONGUESSES} ${infodir}/dir.gz"
INFODIRFILELOCATIONGUESSES="${INFODIRFILELOCATIONGUESSES} /dev/env/DJDIR/info/dir"
INFODIRFILELOCATIONGUESSES="${INFODIRFILELOCATIONGUESSES} /dev/env/DJDIR/info/dir.gz"
INFODIRFILELOCATIONGUESSES="${INFODIRFILELOCATIONGUESSES} /usr/share/info/dir"
INFODIRFILELOCATIONGUESSES="${INFODIRFILELOCATIONGUESSES} /usr/share/info/dir.gz"
INFODIRFILELOCATIONGUESSES="${INFODIRFILELOCATIONGUESSES} /usr/local/share/info/dir"
INFODIRFILELOCATIONGUESSES="${INFODIRFILELOCATIONGUESSES} /usr/local/share/info/dir.gz"
for my_ac_infodirfile in ${INFODIRFILELOCATIONGUESSES}
do
INFODIRFILE=${my_ac_infodirfile}
test -f "$INFODIRFILE" && break
INFODIRFILE=/dev/null
done
if test $INFODIRFILE = /dev/null; then
AC_MSG_RESULT(not found)
AC_MSG_WARN([Could not find Info directory file, set infodirfile in the Makefile by hand])
else
AC_MSG_RESULT($INFODIRFILE)
fi
else
INFODIRFILE=(irrelevant)
fi
AC_SUBST(INFODIRFILE)

dnl Checks for libraries.

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(sys/time.h unistd.h)

dnl Checks for typedefs, structures, and compiler characteristics.
dnl This Autoconf input is compatible with pre-C99 Autoconf, so to
dnl check for long long AC_CHECK_SIZEOF is used to test for a nonzero result,
dnl likewise for _Bool.
AC_C_CONST
AC_CHECK_SIZEOF(long long int,0)
AC_CHECK_SIZEOF(_Bool,0)
AC_HEADER_TIME
AC_EXEEXT
AC_OBJEXT
AC_DECL_YYTEXT
AC_SYS_INTERPRETER

AC_MSG_CHECKING(whether yyrestart() is needed)
# Write a lex program that will cause an error if yyrestart exists.
echo '%%
%%
float yyrestart(int, ...);' | $LEX
AC_TRY_COMPILE(
#include "${LEX_OUTPUT_ROOT}.c"
, exit(0);,
AC_MSG_RESULT(no),
AC_DEFINE(NEED_YYRESTART)
AC_MSG_RESULT(yes))
rm -f ${LEX_OUTPUT_ROOT}.c

if test "$interpval" != "no"; then
AC_DEFINE([HAVE_SYS_INTERPRETER])
fi
AC_DEFINE_UNQUOTED(EXEEXT,"$EXEEXT")

dnl Checks for library functions.
AC_TYPE_SIGNAL
AC_CHECK_FUNCS(gettimeofday gethrtime strdup strtol strtoul)

AC_SUBST(PACKAGE_VERSION)

AC_OUTPUT(Makefile temp/coopt.sh:src/cooptsh.in)
dnl After the output, chmod coopt.sh to be executable.
chmod a+x temp/coopt.sh
