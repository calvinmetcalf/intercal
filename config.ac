dnl -*- autoconf -*-
dnl Process this file with autoconf to produce a configure script.
dnl Yes, that is a valid email address. If your mailer doesn't support nested
dnl comments, then get a better mailer.
AC_INIT([C-INTERCAL],[0.29],[[ais523(524\)x\((525)y)@bham.ac.uk]])
AC_CONFIG_SRCDIR([src/abcess.h])
AC_CONFIG_HEADER(temp/config.h:src/configh.in)

dnl Information about this version of INTERCAL.
PACKAGE_VERSION=0.29

dnl Checks for programs.
AC_PROG_YACC
AC_PROG_CC
AC_PROG_LEX
AC_PROG_RANLIB
AC_CHECK_PROGS(INSTALLINFO,install-info,:)
AC_CHECK_PROGS(MANDB,mandb,:)
AC_CHECK_PROGS(REALPATH,realpath,echo)

dnl Check the location of the Info dir file if we have install-info
if test ! $INSTALLINFO = ":"; then
AC_MSG_CHECKING(the location of the Info dir file)
# Try the following locations in order:
INFODIRFILEPREFIXSAVE=$prefix
if test x$prefix = xNONE; then
prefix=$ac_default_prefix
fi
INFODIRFILELOCATIONGUESSES=$infodir/dir
INFODIRFILELOCATIONGUESSES="$INFODIRFILELOCATIONGUESSES $infodir/dir.gz"
INFODIRFILELOCATIONGUESSES="$INFODIRFILELOCATIONGUESSES $prefix/info/dir"
INFODIRFILELOCATIONGUESSES="$INFODIRFILELOCATIONGUESSES $prefix/info/dir.gz"
INFODIRFILELOCATIONGUESSES="$INFODIRFILELOCATIONGUESSES $prefix/share/info/dir"
INFODIRFILELOCATIONGUESSES="$INFODIRFILELOCATIONGUESSES $prefix/share/info/dir.gz"
for my_ac_temp in 1 2 3 4 5 6; do
eval "INFODIRFILELOCATIONGUESSES=\"$INFODIRFILELOCATIONGUESSES\""
done
prefix=$INFODIRFILEPREFIXSAVE
for my_ac_infodirfile in ${INFODIRFILELOCATIONGUESSES}
do
INFODIRFILE=${my_ac_infodirfile}
test -e "$INFODIRFILE" && break
INFODIRFILE=/dev/null
done
if test $INFODIRFILE = /dev/null; then
AC_MSG_RESULT(not found)
AC_MSG_WARN([Info directory file not found in the --prefix you gave.])
AC_MSG_WARN([Info documentation will not be installed in the main directory tree.])
AC_MSG_WARN([If you want to install in the main directory tree even though it's])
AC_MSG_WARN([outside your --prefix, place a symlink to the directory file from])
AC_MSG_WARN([(your prefix)/share/info/dir and reconfigure.])
INSTALLINFO=:
else
AC_MSG_RESULT($INFODIRFILE)
fi
else
INFODIRFILE=irrelevant
fi
AC_SUBST(INFODIRFILE)

dnl Checks for libraries.

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(sys/time.h unistd.h stdarg.h)
AC_CHECK_HEADERS(stdint.h,[HAVE_STDINT_H=1])
AC_SUBST(HAVE_STDINT_H)

dnl Checks for typedefs, structures, and compiler characteristics.
dnl This Autoconf input is compatible with pre-C99 Autoconf, so to
dnl check for long long AC_CHECK_SIZEOF is used to test for a nonzero result,
dnl likewise for _Bool.
AC_C_CONST
AC_C_VOLATILE
AC_CHECK_SIZEOF(long long int,0)
AC_CHECK_SIZEOF(_Bool,0)
AC_HEADER_TIME
AC_EXEEXT
AC_OBJEXT
AC_SYS_INTERPRETER
AC_TYPE_SIGNAL
AC_TYPE_SIZE_T
AC_TYPE_UINT16_T
AC_TYPE_UINT32_T

AC_MSG_CHECKING(whether yyrestart() is needed)
# Write a lex program that will cause an error if yyrestart exists.
echo '%%
%%
float yyrestart(int, ...);' | $LEX
AC_COMPILE_IFELSE([AC_LANG_PROGRAM([[#include "${LEX_OUTPUT_ROOT}.c"
]], [[exit(0);]])],[AC_MSG_RESULT(no)],[AC_DEFINE(NEED_YYRESTART)
AC_MSG_RESULT(yes)])
rm -f ${LEX_OUTPUT_ROOT}.c

if test "$interpval" != "no"; then
AC_DEFINE([HAVE_SYS_INTERPRETER])
fi
AC_DEFINE_UNQUOTED(EXEEXT,"$EXEEXT")

dnl Checks for library functions.
AC_CHECK_FUNCS(gettimeofday gethrtime strdup strtol strtoul snprintf asprintf)
ICK_SAVECFLAGS=$CFLAGS
CFLAGS="$CFLAGS -lrt"
AC_CHECK_FUNCS(clock_gettime)
CFLAGS=$ICK_SAVECFLAGS

AC_SUBST(PACKAGE_VERSION)

AC_CONFIG_FILES([Makefile temp/coopt.sh:src/cooptsh.in])
AC_OUTPUT
dnl After the output, chmod coopt.sh to be executable.
chmod a+x temp/coopt.sh

dnl Use prebuilt files if necessary. This is done after config.h is generated,
dnl so the touched timestamps fool make in the correct manner.
# Use the prebuilt files if necessary, otherwise delete them.
if test ! -n "$LEX"; then
  touch temp/lexer.c
  AC_WARN([No version of lex was found, using prebuilt lexer.c.])
  AC_WARN([The lexer will not respond to changes in src/lexer.l unless you])
  AC_WARN([install a version of lex and reconfigure.])
else
  rm -f temp/lexer.c
fi
if test ! -n "$YACC"; then
  touch temp/oil.c temp/parser.c temp/parser.h
  AC_WARN([No version of yacc was found, using prebuilt oil.c and parser.c.])
  AC_WARN([The parsers will not respond to changes in source files unless you])
  AC_WARN([install a version of yacc and reconfigure.])
else
  rm -f temp/oil.c temp/parser.c temp/parser.h
fi
